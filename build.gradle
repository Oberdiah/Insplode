buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.android.tools.build:gradle:$androidPluginVersion"
    }
}

tasks.register('writeAllAssetsToFile') {
    doLast {
        // projectfolder/assets
        def assetsFolder = new File("${project.rootDir}/assets/")
        // projectfolder/assets/assets.txt
        def assetsFile = new File(assetsFolder, "assets.txt")
        // delete that file in case we've already created it
        assetsFile.delete()

        // iterate through all files inside that folder
        // convert it to a relative path
        // and append it to assets.txt the file
        fileTree(assetsFolder).collect { assetsFolder.relativePath(it) }.sort().each {
            assetsFile.append(it + "\n")
        }
    }
}

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
}

configure(subprojects - project(':android')) {
    apply plugin: 'java-library'
    apply plugin: 'kotlin'
    sourceCompatibility = 17.0
    compileJava {
        options.incremental = true
    }

    compileJava.dependsOn writeAllAssetsToFile
}

subprojects {
    version = '0.0.2'
    ext.appName = 'Bomb Survival'
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://jitpack.io' }
    }
}

eclipse.project.name = 'BombSurvival' + '-parent'
